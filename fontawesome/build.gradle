apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def packageName = 'info.androidhive'
def libraryVersion = '0.0.5'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.withType(org.gradle.api.tasks.javadoc.Javadoc) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = packageName
            artifactId = project.name
            version = libraryVersion

            // Use the AAR artifact
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            // Source JAR
            task androidSourcesJar(type: Jar) {
                classifier = 'sources'
                from android.sourceSets.main.java.sourceFiles
            }
            artifact androidSourcesJar

            // Javadoc JAR
            task androidJavadocs(type: Javadoc) {
                failOnError false
                source = android.sourceSets.main.java.srcDirs
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            }
            task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
                classifier = 'javadoc'
                from androidJavadocs.destinationDir
            }
            artifact androidJavadocsJar
        }
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license'
        exclude 'META-INF/license.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

